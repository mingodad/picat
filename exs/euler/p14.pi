/*********************************************************
        http://projecteuler.net/problem=14

The following iterative sequence is defined for the set of
positive integers:

    n -> n/2 (n is even)
    n -> 3n + 1 (n is odd)

Using the rule above and starting with 13, we generate the
following sequence:

    13 -> 40 -> 20 -> 10 -> 5 -> 16 -> 8 -> 4 -> 2 -> 1

It can be seen that this sequence (starting at 13 and
finishing at 1) contains 10 terms. Although it has not been
proved yet (Collatz Problem), it is thought that all
starting numbers finish at 1.

Which starting number, under one million, produces the
longest chain?

NOTE: Once the chain starts the terms are allowed to go
above one million.
*********************************************************/

main =>
    max_chain(N,Chain,Len),
    writef("N=%w\nLen=%w%nChain=%w%n",N,Len,Chain).

table (-,-,max)
max_chain(N,Chain,Len) =>
    between(1,1000000,N),
    gen(N,Chain,Len).

table (+,-,-)
gen(1,Chain,Len) => Chain=[1], Len=1.
gen(N,Chain,Len), N mod 2 ==0 => 
    gen(N div 2,Chain1,Len1),
    Chain=[N|Chain1],
    Len=Len1+1.
gen(N,Chain,Len) =>
    gen(3*N+1,Chain1,Len1),
    Chain=[N|Chain1],
    Len=Len1+1.
    
    

