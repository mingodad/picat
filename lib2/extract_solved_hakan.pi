import util.

main([File]) =>
    FD=open(File),             
    FD.read_line()=Line,
    separate(FD,Line,Solved,Unsolved),
    CSolved = [(P,Time) : (P,Time) in Solved, nonvar(Time), to_number(Time) =< 1190],
    printf("COMPLETELY SOLVED = %d:\n",CSolved.length),
	println("========================================"),
    foreach((P,Time) in CSolved)
        printf("%s (%s)\n",P, strip(Time))
    end,
    printf("PARTIALLY SOLVED = %d:\n",Solved.length),
	println("========================================"),	
    foreach((P,Time) in Solved)
        if var(Time) then Time = "unknown" end,
        printf("%s (%s)\n",P, strip(Time))
    end,
    printf("NOT SOLVED=%d:\n",Unsolved.length),
	println("========================================"),	
    foreach((P,Time) in Unsolved)
        if var(Time) then Time = "unknown" end,    
        printf("%s (%s)\n",P, strip(Time))
    end.

separate(_,end_of_file,Solved,Unsolved) => Solved=[],Unsolved=[].
separate(FD,L,Solved,Unsolved),
    start_new_run(L)
=>
    extract_name(L,Name),
    FD.read_line()=Line,    
    solved_or_not(FD,Line,Name,Solved,Unsolved).
separate(FD,_L,Solved,Unsolved) =>
    FD.read_line()=Line,    
    separate(FD,Line,Solved,Unsolved).

solved_or_not(FD,L,Name,Solved,Unsolved),
    (append("real",Time,L); append(Time,[u,s,e,r|_],L)) 
=>
    Unsolved=[(Name,Time)|UnsolvedR],
    FD.read_line()=Line,    
    skip_until_solving(FD,Line,NextLine,_),
    separate(FD,NextLine,Solved,UnsolvedR).
solved_or_not(_FD,end_of_file,Name,Solved,Unsolved) =>
    Solved=[], Unsolved=[(Name,_)].
solved_or_not(FD,L,Name,Solved,Unsolved),
    start_new_run(L)
=>
    Unsolved=[(Name,_)|UnsolvedR],
    separate(FD,L,Solved,UnsolvedR).
solved_or_not(FD,L,Name,Solved,Unsolved),
    (append(_,"=========",_,L); append(_,"---",_,L))
=>
    Solved=[(Name,Time)|SolvedR],
    FD.read_line()=Line,     
    skip_until_solving(FD,Line,NextLine,Time),
    separate(FD,NextLine,SolvedR,Unsolved).
solved_or_not(FD,L,Name,Solved,Unsolved),
    append(_,"UNSAT",_,L)
=>
    printf("UNSAT %s\n",Name),
    Solved=[(Name,Time)|SolvedR],
    FD.read_line()=Line,     
    skip_until_solving(FD,Line,NextLine,Time),
    separate(FD,NextLine,SolvedR,Unsolved).
solved_or_not(FD,_,Name,Solved,Unsolved) =>
    FD.read_line()=Line,     
    solved_or_not(FD,Line,Name,Solved,Unsolved).

skip_until_solving(_FD,end_of_file,NextLine,Time) => NextLine=end_of_file.
skip_until_solving(FD,L,NextLine,Time),
    (append("real",Time,L); append(Time,[u,s,e,r|_],L)) 
=>
    FD.read_line()=L1,
    skip_until_solving(FD,L1,NextLine,_).
skip_until_solving(_FD,L,NextLine,Time),
    start_new_run(L)
 => 
    NextLine=L.
skip_until_solving(FD,_,NextLine,Time) =>
    FD.read_line()=L,
    skip_until_solving(FD,L,NextLine,Time).

extract_name(Str,Name) =>
    once append(_,[BenStr,InsStr,_], split(Str)),
    Name = strip_dir(BenStr) ++ strip_dir(InsStr).

strip_dir(InsStr) = Name,
    append(_,['/'|Post],InsStr)
=>
    Name = strip_dir(Post).
strip_dir(DznStr) = Name =>
    once ((append(Name,".dzn",DznStr); append(Name,".json",DznStr); Name = DznStr)).

start_new_run(L) =>
    append("COMMAND: ",_,L)	